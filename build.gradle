import io.miret.etienne.gradle.sass.CompileSass
import org.yaml.snakeyaml.Yaml

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.yaml:snakeyaml:1.28'
    }
}

plugins {
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'java'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'jacoco'
    id 'org.flywaydb.flyway' version '10.0.1'
    id 'io.miret.etienne.sass' version '1.5.0'
}

jacoco {
    toolVersion = '0.8.10'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def props = new Properties()
if (file(".env").exists()) {
    file(".env").withInputStream { props.load(it) }
}

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    mavenCentral()
}

def profile = props.getProperty('SPRING_PROFILES_ACTIVE')
def formFlowLibraryVersion = '0.0.16-SNAPSHOT'
def useLocalLibrary = System.getenv('USE_LOCAL_LIBRARY')

def applicationYaml = file('src/main/resources/application.yaml')
def applicationProps = new Yaml().load(applicationYaml.text)
def designSystemName = applicationProps.get('form-flow')?.get('design-system')?.get('name')
println "ðŸŽ¨ Using design system: ${designSystemName ?: 'honeycrisp'}"

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
    implementation 'ch.qos.logback.contrib:logback-jackson:0.1.5'

    if (profile == 'dev' || useLocalLibrary == 'true') {
        implementation fileTree(dir: "$rootDir/../form-flow/build/libs", include: '*.jar')
        println "ðŸ“¦ Using local library"
    } else {
        implementation "org.codeforamerica.platform:form-flow:${formFlowLibraryVersion}"
        println "ðŸ“šUsing form flow library ${formFlowLibraryVersion}"
    }

    compileOnly 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.seleniumhq.selenium:selenium-java'
    testImplementation 'io.percy:percy-java-selenium:2.0.2'
    testImplementation 'org.awaitility:awaitility'
    testImplementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.6.2'
    testImplementation 'com.h2database:h2'

    runtimeOnly 'org.postgresql:postgresql'
}

springBoot {
    buildInfo()
}

sass {
    // Version of dart-sass: https://github.com/sass/dart-sass/releases
    version = '1.69.3'
}

tasks.register('cleanGenerated') {
    doFirst {
        delete "generated"
    }
}

tasks.register('checkForNpm', Exec) {
    commandLine 'sh', './scripts/check_for_npm.sh'
}

tasks.register('npmInstall', Exec) {
    dependsOn 'checkForNpm'
    commandLine 'npm', 'install'
}


tasks.register('moveNodeModulesToGenerated', Copy) {
    dependsOn 'npmInstall'
    from 'node_modules'
    into 'generated/main/resources/static'
}

def sassConfiguration = { CompileSass task ->
    // For now, do not load source maps
    task.sourceMap = 'none'
    // Load path
    task.loadPath project.file("${projectDir}/generated/main/resources/static")
    task.loadPath project.file("${projectDir}/generated/main/resources/static/@uswds/uswds/packages")
    // Set the output style:
    // Possible values are â€œexpandedâ€ and â€œcompressedâ€, default is â€œexpandedâ€.
    task.style = 'expanded'
    // Source directory containing sass to compile:
    task.sourceDir = project.file("${projectDir}/src/main/resources/static/assets/scss")
    // Directory where to output generated CSS:
    task.outputDir = project.file("${projectDir}/generated/main/resources/static/assets/css")
}


tasks.register('watchSass', CompileSass) { task ->
    dependsOn installSass
    sassConfiguration(task)
    watch ()
}

// Docs: https://github.com/EtienneMiret/sass-gradle-plugin#usage
tasks.named('compileSass', CompileSass).configure { task ->
    dependsOn 'moveNodeModulesToGenerated'
    sassConfiguration(task)
}

// compileSass needs to run before processResources
processResources {
    if (designSystemName == 'cfa-uswds') {
        dependsOn compileSass
    }
}

clean {
    doFirst {
        delete "generated"
        delete "node_modules"
    }
}

test {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}
